tasks.register('dockerBuild', Exec) {
    dependsOn('assemble')
    def imageName = project.ext.imageName == null ? project.name : project.ext.imageName

    commandLine "docker", "build", "-t", "${imageName}:${project.version}",
            "--build-arg", "LAYERS_DIR=./${project.name}/build/layers",  "${projectDir.parent}"

    doFirst { //prepare layers
        def layersDir = project.layout.buildDirectory.dir('layers').get().asFile
        mkdir(layersDir)
        def bootJar = tasks.named('bootJar', Jar).get().archiveFile.get().asFile
        exec {
            commandLine "java", "-Djarmode=layertools", "-jar", "${bootJar.toString()}",
                    "extract", "--destination", "${layersDir.toString()}"
        }
    }
}
